plugins {
    id 'net.minecraftforge.gradle' version "6.+"
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
//    id 'com.gtnewhorizons.retrofuturagradle' version '1.4.1'
}

version = "${version_major}.${version_minor}.${version_patch}"
group = ''
archivesBaseName = "${name}-${minecraft_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
            jvmArgs '-Dfml.coreMods.load=baubles.coremod.BaublesPlugin'
        }

        client {
            args '--username=InDev'
            workingDirectory file('run')
        }

        server {}


    }

//    legacy {
//        attachMappings = true
//    }
}

fancyGradle {
    patches {
//        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}

repositories {
    mavenCentral()
    maven {
        name "Curse Maven"
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name "Jared's maven"
        url "https://maven.blamejared.com/"
    }
    maven {
        name "CoFH Maven"
        url "https://maven.covers1624.net"
    }
//    maven { url "https://maven.tehnut.info" }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://maven.cleanroommc.com' }
}

dependencies {
    minecraft(
        group  : "${forge_group}",
        name   : "forge",
        version: "${minecraft_version}-${forge_version}"
    )
//    shadow 'zone.rong:mixinbooter:10.6'

    annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
    annotationProcessor 'com.google.guava:guava:32.1.2-jre'
    annotationProcessor 'com.google.code.gson:gson:2.8.9'

    implementation ('zone.rong:mixinbooter:10.6') {
        transitive = false
    }
    annotationProcessor ('zone.rong:mixinbooter:10.6') {
        transitive = false
    }

    runtimeOnly fileTree(dir: 'libs', includes: ['*.jar'])
//    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])

    //Bookshelf
//    runtimeOnly fg.deobf("net.darkhax.bookshelf:Bookshelf-${minecraft_version}:${bookshelf_version}")
    //JEI
    compileOnly fg.deobf("mezz.jei:jei_${minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei_${minecraft_version}:${jei_version}")
    //CoFH
//    runtimeOnly fg.deobf("curse.maven:redstone-flux-270789:2920436")
//    runtimeOnly fg.deobf("curse.maven:cofh-core-69162:2920433")
    compileOnly fg.deobf("curse.maven:cofh-core-69162:2920433")

    //TOP
    runtimeOnly fg.deobf("curse.maven:the-one-probe-community-edition-885670:6736118")
//    runtimeOnly fg.deobf("curse.maven:the-one-probe-245211:2667280")
    //Mo' Bends
    compileOnly fg.deobf("curse.maven:mo-bends-231347:3573346")
    runtimeOnly fg.deobf("curse.maven:mo-bends-231347:3573346")
    //Test dummy
    implementation fg.deobf("curse.maven:mmmmmmmmmmmm-225738:2503041")
    //Botania
//    runtimeOnly fg.deobf("curse.maven:botania-225643:3330934")
//    runtimeOnly fg.deobf("curse.maven:botania-ceu-1179448:6900692")
    //ExU2
    compileOnly fg.deobf("curse.maven:extra-utilities-225561:2678374")
//    runtimeOnly fg.deobf("curse.maven:extra-utilities-225561:2678374")
    //Wings
    compileOnly fg.deobf("curse.maven:wings-302584:2829351")
//    runtimeOnly fg.deobf("curse.maven:wings-302584:2829351")
    //RLArtifacts
    implementation fg.deobf("curse.maven:rl-artifacts-819886:4475428")
}

mixin {
    add sourceSets.main, refmap_configs.replace('${mod_id}', mod_id)

    debug.verbose = true
    debug.export = true
}

tasks.register('copyResource', Copy) {
    group("custom tasks")
    inputs.files sourceSets.main.resources.srcDirs

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.bak', '**/*.psd', 'mcmod.info'
    }
    into 'build/classes/java/main'
}

tasks.register('copyInfo', Copy) {
    group("custom tasks")
    inputs.files 'gradle.properties'

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand mod_id:mod_id, mod_name:mod_name, version:version, mcversion:minecraft_version
    }
    into 'build/classes/java/main'
}

tasks.register('cleanLogs', Delete) {
    group("custom tasks")
    delete("run/crash-reports")
    delete("run/logs")
}

processResources {
    enabled = false
}

classes.configure {
    dependsOn(copyInfo)
    dependsOn(copyResource)
}

jar {
    manifest {
        attributes(
                "FMLCorePlugin": "baubles.coremod.BaublesPlugin",
                'ForceLoadAsMod': 'true',
                'FMLCorePluginContainsFMLMod': 'true',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': '0'
        )
    }

    finalizedBy('reobfJar')
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()
    dependencies {
        exclude 'LICENSE.txt'
        exclude 'org/spongepowered/asm/launch/MixinLaunchPlugin*'
        exclude 'org/spongepowered/asm/launch/MixinTransformationService*'
        exclude 'org/spongepowered/asm/launch/platform/container/ContainerHandleModLauncherEx*'
    }

    finalizedBy('reobfShadowJar')
}

reobf {
    shadowJar {}
}